plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false
    id 'io.franzbecker.gradle-lombok' version '1.14' apply false
}

group "com.vogle.sbpayment"
version "0.0.1-SNAPSHOT"

ext {
    gradleDir = "${rootProject.rootDir}/gradle"
}

subprojects {

    // Sets JAVA
    apply plugin: 'java-library'
    sourceCompatibility = 1.8

    repositories {
        jcenter()
    }

    // Sets dependency manager
    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        dependencies {
            dependency 'org.apache.httpcomponents:httpclient:4.5.6'
            dependency 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.8'
            dependency 'org.hibernate.validator:hibernate-validator:6.0.14.Final'
            dependency 'org.glassfish:javax.el:3.0.1-b11'
            dependency 'ch.qos.logback:logback-classic:1.2.3'

            dependency 'junit:junit:4.12'
            dependency 'org.assertj:assertj-core:3.11.1'
            dependency 'org.mockito:mockito-core:2.23.4'
            dependency 'org.powermock:powermock-api-mockito2:2.0.0'

            dependency 'org.springframework.boot:spring-boot-autoconfigure:2.1.2.RELEASE'
            dependency 'org.springframework.boot:spring-boot-configuration-processor:2.1.2.RELEASE'
            dependency 'org.springframework.boot:spring-boot-starter-test:2.1.2.RELEASE'
        }
    }

    // Sets common dependencies
    dependencies {
        // log
        implementation('ch.qos.logback:logback-classic')

        // test modules
        testImplementation('junit:junit')
        testImplementation('org.assertj:assertj-core')
        testImplementation('org.mockito:mockito-core')
        testImplementation('org.powermock:powermock-api-mockito2')
    }


    // Sets lombok
    apply plugin: 'io.franzbecker.gradle-lombok'
    lombok {
        version = '1.18.4'
    }

    // Sets JavaDoc options
    javadoc {
        source = sourceSets.main.allJava

        options.locale = "en"
        options.charSet = "UTF-8"
        options.encoding = "UTF-8"

        options.use = true
        options.author = true
        options.version = false

        options.bottom = "Â©${new Date().format('yyyy')}, vogle.com"

        options.stylesheetFile = new File(projectDir, "config/javadoc-stylesheet.css")
        options.addStringOption('Xdoclint:none', '-quiet')

    }

}



