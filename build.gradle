description 'Softbank Payment for JAVA'

ext {
    gradleDir = "${rootProject.rootDir}/gradle"

    javaprojects = subprojects.findAll { new File(it.projectDir, 'src/main/java').directory }
    sampleprojects = javaprojects.findAll { !it.name.contains('sample') }
}

// Configuration to apply to all the projects
allprojects {

    group "com.vogle.sbpayment"
    version "0.0.1-SNAPSHOT"

    apply plugin: 'vogle.base'

    contacts {
        'allan@vogle.com' {
            moniker 'Allan Im'
            github 'allanim'
        }
    }

    // Set the maven repository that looks for plugin dependencies.
    repositories {
        jcenter()
    }
}


configure(javaprojects) {

    // Adds Java compilation along with testing and bundling capabilities
    apply plugin: 'vogle.java-library'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // Provides spring dependency management functionality
    dependencyManagement {
        dependencies {
            dependency 'org.apache.httpcomponents:httpclient:4.5.6'
            dependency 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.8'
            dependency 'org.hibernate.validator:hibernate-validator:6.0.14.Final'
            dependency 'org.glassfish:javax.el:3.0.1-b11'
            dependency 'org.slf4j:slf4j-api:1.7.25'
            dependency 'ch.qos.logback:logback-classic:1.2.3'

            dependency 'junit:junit:4.12'
            dependency 'org.assertj:assertj-core:3.11.1'
            dependency 'org.mockito:mockito-core:2.23.4'
            dependency 'org.powermock:powermock-api-mockito2:2.0.0'

            dependency 'org.springframework:spring-core:5.1.4.RELEASE'
            dependency 'org.springframework:spring-context:5.1.4.RELEASE'

            dependency 'org.springframework.boot:spring-boot-autoconfigure:2.1.2.RELEASE'
            dependency 'org.springframework.boot:spring-boot-configuration-processor:2.1.2.RELEASE'
            dependency 'org.springframework.boot:spring-boot-starter-test:2.1.2.RELEASE'
        }
    }

    // Sets common dependencies
    dependencies {
        // log
        api 'org.slf4j:slf4j-api'

        // test modules
        testImplementation 'ch.qos.logback:logback-classic'
        testImplementation 'junit:junit'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.mockito:mockito-core'
        testImplementation 'org.powermock:powermock-api-mockito2'
    }

    // Documentation setup
    javadoc {
        options.bottom = "Copyright ${new Date().format('yyyy')} <a href='http://vogle.com' target='_blank'>Vogle Labs</a>, All rights reserved."
        options.stylesheetFile = new File(rootProject.rootDir, "config/dracula-javadoc8.css")
        options.links("http://docs.oracle.com/javase/8/docs/api/")
    }


    // Performs quality checks on Java source files and generates reports
    apply plugin: 'vogle.code-quality'
    checkstyle {
        config = rootProject.resources.text.fromFile('config/vogle-checkstyle.xml')
    }
    pmd {
        ruleSetConfig = rootProject.resources.text.fromFile('config/vogle-ruleset-6.11.x.xml')
    }


    // Provides code coverage metrics for Java code
    apply plugin: 'vogle.report'
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.5
                }
            }

            rule {
                enabled = false
                element = 'CLASS'
                includes = ['com.vogle.sbpayment*']

                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 0.3
                }
            }
        }
    }


    // Archive task used to create a sources jar and a javadoc jar
    apply plugin: "vogle.archive"


    // Spotless is a general-purpose formatting plugin
//    apply plugin: "com.diffplug.gradle.spotless"
//    spotless {
//
//        java {
//            // Format code using google java format
//            googleJavaFormat()
//
//            // Import order
////            importOrderFile "$rootDir/spotless.importorder"
//
//            // Java Source Header File
////            licenseHeaderFile "$rootDir/spotless.license.java"
//        }
//    }



    // publish
//    apply plugin: 'nebula.maven-publish'
//    apply plugin: 'nebula.maven-manifest'
//    apply plugin: 'nebula.maven-apache-license'
//    apply plugin: "nebula.maven-developer"


}



